apply plugin: "java"

version = '1.0'

sourceSets.main.java.srcDirs = ["Tomodrek"]

repositories{
    mavenCentral()
    // Убираем лишние репозитории, оставляем только jitpack
    maven{ url = 'https://www.jitpack.io' }
}

java{
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_17
}

ext{
    mindustryVersion = 'v155'
    isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

allprojects{
    tasks.withType(JavaCompile).tap{
        configureEach{
            options.compilerArgs.addAll(['--release', '8'])
        }
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:v146"
    compileOnly "com.github.Anuken.Mindustry:core:v146"
}

tasks.register('jarAndroid'){
    dependsOn "jar"
    doLast{
        if(!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set.")
        def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find{ f -> new File(f, "android.jar").exists() }
        if(!platformRoot) throw new GradleException("No android.jar found.")
        def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect{ "--classpath $it.path" }.join(" ")
        def d8 = isWindows ? "d8.bat" : "d8"
        "$d8 $dependencies --min-api 14 --output ${project.name}Android.jar ${project.name}Desktop.jar"
                .execute(null, new File("build/libs")).waitForProcessOutput(System.out, System.err)
    }
}

jar{
    archiveFileName = "${project.name}Desktop.jar"
    from{
        configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    }
    from(projectDir){
        include "mod.json" // или mod.hjson
    }
    from("assets/"){
        include "**"
    }
}

tasks.register('deploy', Jar){
    dependsOn jarAndroid
    dependsOn jar
    archiveFileName = "${project.name}.jar"
    from{ [zipTree("build/libs/${project.name}Desktop.jar"), zipTree("build/libs/${project.name}Android.jar")] }
    doLast{
        delete{
            delete "build/libs/${project.name}Android.jar"
        }
    }
}
